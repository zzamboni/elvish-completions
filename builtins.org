#+TITLE:  Elvish completions for various built-in commands
#+AUTHOR: Diego Zamboni
#+EMAIL:  diego@zzamboni.org

#+name: module-summary
Completions for some of Elvish's built-in commands, including =use=, the =epm= module and =elvish= itself.

This file is written in [[https://leanpub.com/lit-config][literate programming style]], to make it easy to explain. See [[file:$name.elv][$name.elv]] for the generated file.

* Table of Contents                                            :TOC:noexport:
- [[#usage][Usage]]
- [[#implementation][Implementation]]
  - [[#use][use]]
  - [[#epm][epm]]
  - [[#elvish][elvish]]

* Usage

Install the =elvish-modules= package using [[https://elvish.io/ref/epm.html][epm]]:

#+begin_src elvish
use epm
epm:install github.com/zzamboni/elvish-completions
#+end_src

In your =rc.elv=, load this module:

#+begin_src elvish
use github.com/zzamboni/elvish-completions/builtins
#+end_src

Included completions:

- =elvish= (completes command-line options and =.elv= files).
- =use= (completes all modules in =~/.elvish/lib=). Example (the "lack module name" message is normal and part of Elvish's as-you-type compilation, it goes away when you add a module name):
  #+begin_example
    [~]â”€> use <Tab>
     COMPLETING argument _
    compilation error: 3-3 in [tty]: lack module name
     elvish-dev/epm/epm                               github.com/zzamboni/elvish-modules/alias
     github.com/iwoloschin/elvish-packages/powernerd  github.com/zzamboni/elvish-modules/atlas
     github.com/iwoloschin/elvish-packages/python     github.com/zzamboni/elvish-modules/bang-bang
     github.com/iwoloschin/elvish-packages/update     github.com/zzamboni/elvish-modules/dir
     github.com/muesli/elvish-libs/git                github.com/zzamboni/elvish-modules/git-vcsh
     github.com/muesli/elvish-libs/theme/muesli       github.com/zzamboni/elvish-modules/long-running-notifications
     github.com/muesli/elvish-libs/theme/powerline    github.com/zzamboni/elvish-modules/nix
     github.com/xiaq/edit.elv/compl/go                github.com/zzamboni/elvish-modules/opsgenie
     github.com/xiaq/edit.elv/smart-matcher           github.com/zzamboni/elvish-modules/prompt-hooks
     github.com/zzamboni/elvish-completions/builtins  github.com/zzamboni/elvish-modules/proxy
     github.com/zzamboni/elvish-completions/cd        github.com/zzamboni/elvish-modules/semver
     github.com/zzamboni/elvish-completions/comp      github.com/zzamboni/elvish-modules/terminal-title
     github.com/zzamboni/elvish-completions/git       github.com/zzamboni/elvish-modules/util
     github.com/zzamboni/elvish-completions/ssh       github.com/zzamboni/elvish-themes/chain
     github.com/zzamboni/elvish-completions/vcsh      private
  #+end_example
- =epm= commands: completes installed packages for the appropriate commands.

* Implementation
:PROPERTIES:
:header-args:elvish: :tangle (concat (file-name-sans-extension (buffer-file-name)) ".elv")
:header-args: :mkdirp yes :comments no
:END:

Load the completion framework and other libraries.

#+begin_src elvish
  use ./comp
  use re
  use path
  use str
#+end_src

** use

Completer for the =use= command, which includes all modules in =~/.elvish/lib/=, but also allows completing relative paths starting with =./= or =../=.

#+begin_src elvish
  set edit:completion:arg-completer[use] = (
    comp:sequence [
      {|stem|
        if (not (str:has-prefix $stem '.')) {
          put './' '../'
          put ~/.elvish/lib/**[nomatch-ok].elv | each {|m|
            if (not (path:is-dir $m)) {
              re:replace ~/.elvish/lib/'(.*).elv' '$1' $m
            }
          }
        } else {
          if (eq $stem ".") { set stem = "./" }
          if (eq $stem "..") { set stem = "../" }
          comp:files $stem &regex='.*\.elv' &transform={|s| re:replace '\.elv$' '' $s }
        }
      }
    ]
  )
#+end_src

** epm

Completers for the =epm= commands.

#+begin_src elvish
  use epm

  var epm-completer-one  = (comp:sequence [ $epm:list~ ])
  var epm-completer-many = (comp:sequence [ $epm:list~ ...])
  set edit:completion:arg-completer[epm:query]     = $epm-completer-one
  set edit:completion:arg-completer[epm:metadata]  = $epm-completer-one
  set edit:completion:arg-completer[epm:dest]      = $epm-completer-one
  set edit:completion:arg-completer[epm:uninstall] = $epm-completer-many
  set edit:completion:arg-completer[epm:upgrade]   = $epm-completer-many
#+end_src

** elvish

Completer for the =elvish= command.

#+begin_src elvish
  set edit:completion:arg-completer[elvish] = (comp:sequence ^
    &opts= { elvish -help | comp:extract-opts &fold } ^
    [ {|arg| comp:files $arg &regex='\.elv$' } ] ^
  )
#+end_src
